/*
 * generated by Xtext 2.10.0
 */
package fr.minesnantes.browserautomation.generator

import fr.minesnantes.browserautomation.seleniumDSL.Procedure
import fr.minesnantes.browserautomation.seleniumDSL.SeleniumTest
import java.io.File
import org.eclipse.emf.common.util.EList
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import fr.minesnantes.browserautomation.seleniumDSL.Navigate
import fr.minesnantes.browserautomation.seleniumDSL.Click
import fr.minesnantes.browserautomation.seleniumDSL.Instruction
import fr.minesnantes.browserautomation.seleniumDSL.Fill
import fr.minesnantes.browserautomation.seleniumDSL.Select
import fr.minesnantes.browserautomation.seleniumDSL.Tick
import java.util.HashMap
import fr.minesnantes.browserautomation.seleniumDSL.Read
import java.util.ArrayList
import java.util.List

class Counter {
    private int count;

    def Counter() {
        count = 0;
    }

    def int nextCount() {
        return count++;
    }
}

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class SeleniumDSLGenerator extends AbstractGenerator {

    private EList<Procedure> procedures;
    
    private List<String> variables;

    private Counter elementCounter;
    
    def initializeContext() {
        this.elementCounter = new Counter();
        this.variables = new ArrayList<String>();
    }
    
    def destroyContext() {
        this.elementCounter = null;
        this.variables = null;
    }

    override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
        initializeContext
        
        fsa.generateFile(
            'browserautomation' + File.separator + 'SeleniumTest.java',
            resource.contents.filter(SeleniumTest).head.generateSeleniumTest
        )
        
        destroyContext
    }

    def generateSeleniumTest(SeleniumTest st) '''
        package browserautomation;
        
        import org.openqa.selenium.By;
        import org.openqa.selenium.WebDriver;
        import org.openqa.selenium.WebElement;
        import org.openqa.selenium.chrome.ChromeDriver;
        import org.openqa.selenium.support.ui.Select;
        
        public class SeleniumTest {
            public static void main(String[] args) {
                // Initialize Selenium web driver for Google Chrome
                System.setProperty("webdriver.chrome.driver", "lib/chromedriver");
                WebDriver webDriver = new ChromeDriver();
                
                «FOR i : st.main.instructions»
                    «generateInstruction(i)»
                «ENDFOR»
                
                // Close the browser
                webDriver.quit();
            }
        }
    '''
    
    // Dispatch methods to handle defined instructions    
    def dispatch generateInstruction(Navigate n) '''
        webDriver.get("«n.url»");
    '''
    
    def dispatch generateInstruction(Click c) '''
        «val eltName = '''element«elementCounter.nextCount»'''»
        «val clickValue = c.value»
        «switch c.type {
            case 'input': '''WebElement «eltName» = webDriver.findElement(By.xpath("//input[@value=\"«clickValue»\"]"));'''
            case 'link': '''WebElement «eltName» = webDriver.findElement(By.linkText("«clickValue»"));'''
            case 'xpath': '''WebElement «eltName» = webDriver.findElement(By.xpath("«clickValue»"));'''
            case 'name' : '''WebElement «eltName» = webDriver.findElement(By.name("«clickValue»"));'''
            default: '''// FIXME unrecognized click instruction: click «c.type» «clickValue»''' 
        }»
        «eltName».click();
    '''
    
    def dispatch generateInstruction(Fill f) '''
        «val eltName = '''element«elementCounter.nextCount»'''»
        WebElement «eltName» = webDriver.findElement(By.name("«f.name»"));
        «eltName».sendKeys(«IF variables.contains(f.value)»«f.value»«ELSE»"«f.value»"«ENDIF»);
    '''
    
    def dispatch generateInstruction(Tick t) '''
        «val eltName = '''element«elementCounter.nextCount»'''»
        WebElement «eltName» = webDriver.findElement(By.name("«t.name»"));
        «eltName».click();
    '''
    
    def dispatch generateInstruction(Select s) '''
        «val eltName = '''element«elementCounter.nextCount»'''»
        Select «eltName» = new Select(webDriver.findElement(By.name("«s.name»")));
        «eltName».selectByVisibleText("«s.value»");
    '''
    
    def dispatch generateInstruction(Read r) {
        variables.add(r.variable)
        '''
            «val eltName = '''element«elementCounter.nextCount»'''»
            WebElement «eltName» = webDriver.findElement(By.name("«r.name»"));
            String «r.variable» = «eltName».getAttribute("value");
        '''
    }
    
        def dispatch generateInstruction(Instruction i) '''
        // FIXME wtf is this instruction?
    '''

}
